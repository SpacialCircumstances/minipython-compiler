use crate::ast::{Ast, Program};
use crate::name::{InternedName, NameStore};
use crate::lexer::{Token, Location, LexerError};
use crate::lexer::Token::*;

grammar<'input>(input: &'input str, names: &mut NameStore);

extern {
    type Location = Location;
    type Error = LexerError;

    enum Token<'input> {
        "input" => Input,
        "output" => Output,
        "," => Comma,
        "indent" => Indent,
        "unindent" => Unindent,
        "return" => Return,
        "def" => Def,
        ":" => Colon,
        "name" => Name(<&'input str>),
        "(" => OpenParen,
        ")" => CloseParen,
        "while" => While,
        "!=0" => NotEqualZero,
        "+=1" => PlusEqualOne,
        "-=1" => MinusEqualOne,
        "=" => Equal
    }
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Program: Program = <ins:Input> <outs:Output> <body:Block> => Program {
    inputs: ins,
    output: outs,
    body: body
};

Input: Vec<InternedName> = "input" ":" <ArgList>;

Output: InternedName = "output" ":" <Name>;

pub Block: Vec<Ast> = Expr*;

Expr: Ast = {
    <Name> "+=1" => Ast::Incr(<>),
    <Name> "-=1" => Ast::Decr(<>),
    "return" <Name> => Ast::Return(<>),
    WhileExpr,
    Def,
    Assign
};

WhileExpr: Ast = "while" <n:Name> "!=0" ":" "indent" <p:Expr+> "unindent" => Ast::While { cond_var: n, body: p };

Assign: Ast = <vname:Name> "=" <fname:Name> "(" <args:ArgList> ")" => Ast::Assign { var_name: vname, fun_name: fname, args: args };

Def: Ast = "def" <fname:Name> "(" <params:ArgList> ")" ":" "indent" <p:Expr+> "unindent" => Ast::Def { name: fname, parameters: params, body: p };

ArgList: Vec<InternedName> = Comma<Name>;

Name: InternedName = "name" => names.register(<>);